eset LevelsNutriments = {l1,l2,l3,l4}.
     LevelWarterJar  = {water_empty, water_low, water_medium, water_full}.
     Luminosity       = {low_light, medium_light, high_light}.
     Temperature      = {cold, normal, hot}.
     HealthPlant       = {healthy,low_water,low_nutr,cold_stressed,hot_stressed}.
     NutrimeChief     = {active, inactive}.
     Colors           = {red,green,blue}.
     ScreenState      = {scr_ok, scr_low_water, scr_low_nutr ,scr_hot,scr_cold,scr_vent_on}.
     FanState         ={fan_off,fan_on}.
     UIPos = { jar_pos, plant_pos, phone_pos, fan_pos, pump_pos, led_pos, nutr_ind_pos }.

eqn
    xPos(jar_pos)      = 620.   yPos(jar_pos)      = 557.
    xPos(plant_pos)    = 820.   yPos(plant_pos)    = 420.
    xPos(phone_pos)    = 20.   yPos(phone_pos)    = 520.
    xPos(fan_pos)      = 1000.  yPos(fan_pos)      = 220.
    xPos(pump_pos)     = 770.   yPos(pump_pos)     = 332.
    xPos(led_pos)      = 440.   yPos(led_pos)      = 220.
    xPos(nutr_ind_pos) = 600.   yPos(nutr_ind_pos) = 557.

    afterLevel(l1) = l2.  beforeLevel(l1) = l1.
    afterLevel(l2) = l3.  beforeLevel(l2) = l1.
    afterLevel(l3) = l4.  beforeLevel(l3) = l2.

                        

scene ppScene = {

    size = (1251,757).
    layers = { top }.

    background = loadImage(Images/hydrobackground.png).
    jar_empty_img   = loadImage(Images/jar_empty.png).
    jar_low_img   = loadImage(Images/jar_low.png).
    jar_medium_img   = loadImage(Images/jar_medium.png).
    jar_full_img   = loadImage(Images/jar_full.png).
        
    plant_healthy_img     = loadImage(Images/plant_healthy.jpg).
    plant_low_water_img   = loadImage(Images/plant_low_water.png).
    plant_hot_stress_img  = loadImage(Images/plant_hot_stress.jpg).
    plant_cold_stress_img = loadImage(Images/plant_cold_stress.jpg).
    plant_low_nutr_img    = loadImage(Images/plant_low_nutr.jpg).

    ind_low_nutr1_img    = loadImage(Images/ind_low_nutr1.png).
    ind_low_nutr2_img    = loadImage(Images/ind_low_nutr2.png).
    ind_low_nutr3_img    = loadImage(Images/ind_low_nutr3.png).
    ind_low_nutr4_img    = loadImage(Images/ind_low_nutr4.png).
    

    led_blue_img  = loadImage(Images/led_blue.png).
    led_green_img = loadImage(Images/led_green.png).
    led_red_img   = loadImage(Images/led_red.png).

    phone_ok_img        = loadImage(Images/phone_ok.png).
    phone_low_water_img = loadImage(Images/phone_low_water.png).
    phone_low_nutr_img  = loadImage(Images/phone_low_nutr.png).
    phone_hot_img       = loadImage(Images/phone_hot.png).
    phone_cold_img      = loadImage(Images/phone_cold.png).
    phone_vent_on_img   = loadImage(Images/phone_vent_on.png).

    fan_off_img = loadImage(Images/fan_off.png).
    fan_on_img  = loadImage(Images/fan_on.png).

    pump_img    = loadImage(Images/pum_on.png).

    widget jar = {attributes = {level in LevelWarterJar.} display = {
        level = water_empty -> jar_empty_img.
        level = water_low -> jar_low_img.
        level = water_medium -> jar_medium_img.
        level = water_full -> jar_full_img.
    } init = { 
        wdL = top.
        level = water_full.
    }}

    widget plant = {attributes = {health in HealthPlant.} display = {
        health = healthy -> plant_healthy_img.
        health = low_water -> plant_low_water_img.
        health = low_nutr -> plant_low_nutr_img.
        health = cold_stressed -> plant_cold_stress_img.
        health = hot_stressed -> plant_hot_stress_img.
    } init = { 
        wdL = top.
        health = healthy.
    }}

    widget ind_low_nutr = {attributes = {ind in LevelsNutriments.} display = {
        ind = l1 -> ind_low_nutr1_img.
        ind = l2 -> ind_low_nutr2_img.
        ind = l3 -> ind_low_nutr3_img.
        ind = l4 -> ind_low_nutr4_img.
    } init = { 
        wdL = top.
        ind = l1.
    }}

    widget led =  {attributes = {light in Colors.} display = {
        light = blue -> led_blue_img.
        light = green -> led_green_img.
        light = red -> led_red_img.
    } init = { 
        wdL = top.
        light = green.
    }}

    widget phone = {attributes = {screen in ScreenState.} display = {
        screen = scr_ok -> phone_ok_img.
        screen = scr_low_water -> phone_low_water_img.
        screen = scr_low_nutr -> phone_low_nutr_img.
        screen = scr_hot -> phone_hot_img.
        screen = scr_cold -> phone_cold_img.
        screen = scr_vent_on -> phone_vent_on_img.
    } init = { 
        wdL = top.
        screen = scr_ok.
    }}

    widget fan = {attributes = {state in FanState.} display = {
        state = fan_off -> fan_off_img.
        state = fan_on -> fan_on_img.
    } init = { 
        wdL = top.
        state = fan_off.
    }}

    widget pump = { display = { pump_img } init = { wdL = top. } }

}.
    



proc Init =
    draw_scene(ppScene);

    place_at(jar,  ppScene, xPos(jar_pos), yPos(jar_pos));
    tell(water_full);

    place_at(plant, ppScene, xPos(plant_pos), yPos(plant_pos));
    tell(healthy);

    place_at(phone, ppScene, xPos(phone_pos), yPos(phone_pos));
    tell(scr_ok);

    place_at(fan, ppScene, xPos(fan_pos), yPos(fan_pos));
    tell(fan_off);

    place_at(pump, ppScene, xPos(pump_pos), yPos(pump_pos));

    place_at(led, ppScene, xPos(led_pos),  yPos(led_pos));
    tell(green);
    tell(normal);

    place_at(ind_low_nutr, ppScene, xPos(nutr_ind_pos), yPos(nutr_ind_pos));
    tell(l1);

    show(plant, ppScene);

    show(jar, ppScene);

    show(ind_low_nutr, ppScene);

    show(led, ppScene);

    show(phone, ppScene);

    show(fan, ppScene);

    show(pump, ppScene).

    SetPlantHealthReplace(new_health : HealthPlant) = 
        (ask(new_health)) +
        (nask(new_health);
        (get(healthy)+get(low_water)+get(low_nutr)+
        get(cold_stressed)+get(hot_stressed));
        att(health, plant, ppScene, new_health);
        tell(new_health)).

    SetPlantHealthAdd(new_health : HealthPlant) =
        (ask(new_health)) +
        (nask(new_health);
        (get(healthy) + nask(healthy));
        att(health, plant, ppScene, new_health);
        tell(new_health)).

    SetPlantHealthNext =
        (ask(low_water);att(health, plant, ppScene, low_water)) +
        (ask(low_nutr);att(health, plant, ppScene, low_nutr)) +
        (ask(cold_stressed);att(health, plant, ppScene, cold_stressed)) +
        (ask(hot_stressed);att(health, plant, ppScene, hot_stressed)) +
        (nask(low_water);nask(low_nutr);nask(cold_stressed);nask(hot_stressed);tell(healthy);att(health, plant, ppScene, healthy) ).

    SetJarLevel(new_level : LevelWarterJar) =
        (ask(new_level)) +
        (nask(new_level);
        (get(water_empty)+ get(water_low)+ get(water_medium)+get(water_full));
        att(level, jar, ppScene, new_level);
        tell(new_level);
        ((new_level = water_low -> SetPlantHealthAdd(low_water); SetPhoneScreenAdd(scr_low_water)) +
        (new_level = water_empty -> SetPlantHealthAdd(low_water); SetPhoneScreenAdd(scr_low_water)) +
        (new_level = water_medium -> (get(scr_low_water);get(low_water)) + (nask(scr_low_water); nask(low_water));SetPhoneScreenNext; SetPlantHealthNext) +
        (new_level = water_full -> (get(scr_low_water);get(low_water)) + (nask(scr_low_water); nask(low_water));SetPhoneScreenNext; SetPlantHealthNext)
        )).

    SetIndNut(new_ind : LevelsNutriments) =
        (ask(new_ind)) +
        (nask(new_ind);
        (get(l1)+ get(l2)+ get(l3) +
        (get(l4); get(scr_low_nutr); get(low_nutr) ));
        att(ind, ind_low_nutr, ppScene, new_ind);
        tell(new_ind);
        (new_ind = l4 -> SetPlantHealthAdd(low_nutr);SetPhoneScreenAdd(scr_low_nutr)) +
        (nask(l4);SetPhoneScreenNext; SetPlantHealthNext)).

    SetLedLight(new_light : Colors) = 
        (ask(new_light)) +
        (nask(new_light);
        (get(red)+get(green)+get(blue));
        att(light, led, ppScene, new_light);
        tell(new_light)).

    SetPhoneScreenReplace(new_screen : ScreenState) =
        (ask(new_screen)) +
        (nask(new_screen);
        (get(scr_ok)+get(scr_low_water)+get(scr_low_nutr)+
        get(scr_hot)+get(scr_cold)+get(scr_vent_on));
        att(screen, phone, ppScene, new_screen);
        tell(new_screen)).

    SetPhoneScreenAdd(new_screen : ScreenState) =
        (ask(new_screen)) +
        (nask(new_screen);
        (get(scr_ok) + nask(scr_ok));
        att(screen, phone, ppScene, new_screen);
        tell(new_screen)).

    SetPhoneScreenNext = 
        (ask(scr_low_water);att(screen, phone, ppScene, scr_low_water)) +
        (ask(scr_low_nutr);att(screen, phone, ppScene, scr_low_nutr)) +
        (ask(scr_hot);att(screen, phone, ppScene, scr_hot)) +
        (ask(scr_cold);att(screen, phone, ppScene, scr_cold)) +
        (ask(scr_vent_on);att(screen, phone, ppScene, scr_vent_on)) +
        (nask(scr_low_water);nask(scr_low_nutr);nask(scr_hot);nask(scr_cold);nask(scr_vent_on);tell(scr_ok); att(screen, phone, ppScene, scr_ok)).

    SetFanState(new_state : FanState) =
        (ask(new_state)) +
        (nask(new_state);
        (get(fan_off) + get(fan_on));
        att(state, fan, ppScene, new_state);
        tell(new_state);
        (new_state = fan_on -> SetPhoneScreenAdd(scr_vent_on) +
        (get(scr_vent_on);SetPhoneScreenNext))).

    SetTemperature(new_temp : Temperature) =
        (ask(new_temp)) +
        (nask(new_temp);
        ((get(cold);get(cold_stressed);get(scr_cold)) + (get(hot);get(hot_stressed);get(scr_hot)) + get(normal));
        tell(new_temp);
        ((new_temp = cold -> SetPlantHealthAdd(cold_stressed); SetPhoneScreenAdd(scr_cold); SetLedLight(blue)) +
        (new_temp = hot -> SetPlantHealthAdd(hot_stressed); SetPhoneScreenAdd(scr_hot); SetLedLight(red)) +
        (new_temp = normal -> SetLedLight(green)))).


    RandomModifications = 
        (SetJarLevel(water_full) + SetJarLevel(water_medium) + SetJarLevel(water_low) + SetJarLevel(water_empty) +
        SetIndNut(l1) + SetIndNut(l2) + SetIndNut(l3) + SetIndNut(l4) +
        SetTemperature(normal) + SetTemperature(cold) + SetTemperature(hot)).